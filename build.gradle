group 'com.itstimetosnuff.forrest.bot'
version '1.2.2' /*TODO: Reminder - if version changed Procfile must be changed to*/

wrapper {
    gradleVersion = '5.6.2'
}

configure(allprojects) { project->
    apply plugin: 'java'
    apply from: "${rootProject.projectDir}/etc/repositories.gradle"
    repositories extRepositories
    apply from: "${rootProject.projectDir}/etc/dependencies.gradle"
    apply from: "${rootProject.projectDir}/etc/analyze.gradle"

    sourceCompatibility = 1.8
    repositories extRepositories

    task stage {
        dependsOn build
    }

    test {
        useJUnitPlatform {
            excludeTags 'slow'
            includeEngines 'junit-jupiter'
        }

        testLogging {
            events 'PASSED', 'FAILED', 'SKIPPED'
        }

        failFast = false
    }

    dependencies {
        annotationProcessor utils.lombok

        compileOnly utils.lombok

        compile log.slf4jLog4j
        compile telegram.bots
        compile google.calendar
        compile google.sheets
        compile json.gson

        testAnnotationProcessor utils.lombok

        testCompileOnly utils.lombok

        testCompile testing.junit5
        testCompile testing.junit5Mockhito
    }

    clean.doFirst {
        delete "${project.projectDir}/out/"
    }
}

project('forrest-bot-main') {

    dependencies {
        compile project(':forrest-bot-google')
    }
}

project('forrest-bot-google') {

    dependencies {

    }
}

configurations {
    childJars
}

dependencies {
    subprojects.each { childJars project(it.path)}
}

jar {
    enabled = true

    manifest {
        attributes ('Implementation-Title': rootProject.name,
                'Implementation-Version': archiveVersion,
                'Main-Class': 'com.itstimetosnuff.forrest.bot.BotStarter')
    }

    dependsOn configurations.childJars

    from { configurations.childJars.collect { zipTree(it) } }
}
