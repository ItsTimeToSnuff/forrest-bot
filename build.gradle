group 'com.itstimetosnuff.forrest.bot'
version '1.1'

wrapper {
    gradleVersion = '5.6.2'
}

configure(allprojects) { project->
    apply plugin: 'java'
    apply from: "${rootProject.projectDir}/etc/repositories.gradle"
    repositories extRepositories
    apply from: "${rootProject.projectDir}/etc/dependencies.gradle"
    apply from: "${rootProject.projectDir}/etc/analyze.gradle"

    sourceCompatibility = 1.8
    repositories extRepositories

    task stage {
        dependsOn build
    }

    test {
        useJUnitPlatform {
            excludeTags 'slow'
            includeEngines 'junit-jupiter'
        }
        testLogging {
            events 'PASSED', 'FAILED', 'SKIPPED'
        }
        failFast = false
    }

    dependencies {
        annotationProcessor utils.lombok

        compileOnly utils.lombok

        compile log.slf4jLog4j
        compile telegram.bots
        compile google.calendar
        compile google.sheets
        compile json.gson

        testAnnotationProcessor utils.lombok

        testCompileOnly utils.lombok

        testCompile testing.junit5
        testCompile testing.junit5Mockhito
    }

    clean.doFirst {
        delete "${project.projectDir}/out/"
    }
}

project('forrest-bot-main') {

    jar {
        enabled = true
        manifest {
            attributes ('Implementation-Title': 'Gradle Jar File Example',
                    'Implementation-Version': '1.0',
                    'Main-Class': 'com.itstimetosnuff.forrest.bot.BotStarter')
        }
        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    }

    dependencies {
        compile project(':forrest-bot-google')
    }
}

project('forrest-bot-google') {

    dependencies {
    }
}
